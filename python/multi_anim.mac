# ----------------------------------------------------------------
# Swinging Color Bars v.1.0 2/0/2019 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer
%play-macro bars-app

$num-instances 4
$width 8
#$HSL-ANGLE 4
$background-width 4
$background-time 45
$background-sat 63
$background-level 4
$background-step 1

$base-next-time 20
$draw-mode <DRAW-WRITE>
$brightness <BRIGHTNESS-AUTO>
$render-time <base-next-time>
$fade-rate 9000
$low-limit `<width>/2`
$high-limit `<NUM-LEDS>-<low-limit>`
$add-time 3

%allow-mutability
$HSL-TIME 1000

[bars-app 10]
    start-application
 
<<< <draw-mode> != <DRAW-WRITE>
    # set the preset draw mode
    <draw-mode>
    set-draw-mode
>>>

<<< <brightness> != <BRIGHTNESS-AUTO>
    # set the preset brightness level
    <brightness>
    set-brightness
>>>

    # start the macro instances
    [[[start <num-instances>]]]

    # start up the palette color sequencer
    (hsl-sequencer-start)

    # schedule the display rendering
    (render-all <render-time>)

    (background-start)

[render-all]
    {{{
        (erase-all)
        [[[draw <num-instances>]]]
    }}}

[background-start]
    {seq-bg},<background-width>
    wheel-sequencer
    (background-advance <background-time>)

[background-advance]
    {seq-bg},<GET-NEXT>,<background-step>
    advance-sequence

[erase-draw]
    {seq-bg},<GET-CURRENT>
    get-sequence
    set-position
    {{
        <background-level>
        push
        <background-sat>
        push
        {seq-hsl},<GET-CURRENT>
        get-sequence
        push
        recall
        add-hsl-color
    }}

[erase-all]
    <FAST-ERASE-BUFFER>
    erase-buffer

    (erase-draw)

    `<NUM-LEDS>/2`
    set-window
    {{
        `<background-width>*-1`
        duplicate
    }}

    mirror

#    (hsl-sequencer)

(((bar <num-instances> INSTANCE `INSTANCE*<add-time>+<base-next-time>`)))

[[bar INSTANCE PALETTE TIME

    [start-INSTANCE]
        {seq-INSTANCE},<high-limit>,<low-limit>
        wheel-cosine-sequencer
        (advance-INSTANCE TIME)

    [advance-INSTANCE]
        {seq-INSTANCE},<GET-NEXT>
        advance-sequence

    [draw-INSTANCE]
        <low-limit>
        push

        {seq-INSTANCE},<GET-COMPUTED>
        get-sequence
        push
        sub-equals

        <width>
        recall

        set-position
        {{
            PALETTE
            add-palette-color
            darken
            <FILL-COLORS>
            fill
        }}
]]

