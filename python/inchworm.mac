# ----------------------------------------------------------------
# Inchworm
# 5 rainbow wandering inchworms
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

%play-macro inchworm-app

$render-time 100
$width0 `<NUM-LEDS> * 1 / 6`
$width1 <NUM-LEDS>
$width2 `<NUM-LEDS> * 5 / 6`
$time1 50
$time2 200
$seq-type swing-sequencer
$fade-type fast-fade
$num-worms 5

[inchworm-app 10]
  start-application

  # make colors intermingle
  <DRAW-PLUS>
  set-draw-mode

  # start the worms
  [[[worm-start <num-worms>]]]
  
  # start the palette color sequencer
  (hsl-sequencer-start)

  # schedule the renderer
  (inchworm-render <render-time>)

#  <EFFECTS-PROCESSING>
#  continue

[inchworm-render]
  <EFFECTS-PROCESSING>
  refresh-display

# expand 5 meta-templates for worms, computing the various values
(((worm <num-worms> `<time1>-(INSTANCE*5)` `<time2>-(INSTANCE*5)` INSTANCE `INSTANCE+1` INSTANCE `INSTANCE+5`)))

# template for all worm processing
# INSTANCE - worm number
# TIME1 - fast advance time
# TIME2 - slow advance time
# COLOR - palette color index
# STEP - advance step for the slow advancing sequencer
# SEQ1 - fast advancing sequencer number
# SEQ2 - slow advancing sequencer number
[[worm INSTANCE TIME1 TIME2 COLOR STEP SEQ1 SEQ2

  # start up a worm
  [worm-start-INSTANCE]
    # allocate a sequencer 1/6 the display width for the small/fast sequence
    {seq-SEQ1},<width0>
    <seq-type>

    # allocate a sequencer for the remaining 5/6 display width to swing the sequence
    {seq-SEQ2},<width2>
    <seq-type>

    # schedule the draw macro
    (worm-draw-INSTANCE TIME1)
    
    # schedule the advance macro
    (worm-advance-INSTANCE TIME2)

  # draw the next animation frame
  [worm-draw-INSTANCE]
    # advance the 1/6 sequencer
    <seq-SEQ1>,<GET-NEXT-NO-SAVE>
    advance-sequence

    # add the two sequencers
    # get and set the offset,window to fill in for this frame
    <seq-SEQ1>,<ADD-SEQ>,<seq-SEQ2>
    sequence-next-window

    # set drawing window
    set-position
    {{
        # place the palette color
        COLOR
        push
	(worm-draw)
    }}

  # macro to sequence the 5/6 sequencer at a variable rate
  [worm-advance-INSTANCE]
    <seq-SEQ2>,<GET-NEXT-NO-SAVE>,STEP
    advance-sequence
]]

[worm-draw]
  pop
  add-palette-color

  # set the fade type
  <fade-type>

  # fill the whole space
  fill

