# ----------------------------------------------------------------
# Inchworm
# 5 rainbow wandering inchworms
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

%play-macro inchworm-app

$render-time 100
$width0 `<NUM-LEDS> * 1 / 6`
$width1 <NUM-LEDS>
$width2 `<NUM-LEDS> * 5 / 6`
$time1 50
$time2 200
$seq-type sqs
$fade-type ffd

[inchworm-app 10]
  application

  # make colors intermingle
  <DRAW-PLUS>
  draw-mode

  # schedule the renderer
  (render <render-time>)
  
  # start the worms
  (worms-start)
  
  # start the palette color sequencer
  (hsl-sequencer-start)

# start up all worms
[worms-start]
  # expand 5 wornm start meta-templates
  (((worm-start 5)))

# expand 5 meta-templates for worms, computing the various values
(((worm 5 `<time1>-(INSTANCE*5)` `<time2>-(INSTANCE*5)` INSTANCE `INSTANCE+1` INSTANCE `INSTANCE+5`)))

# template for starting worms
# INSTANCE - worm number
[[worm-start INSTANCE
  (worm-start-INSTANCE)
]]

# template for all worm processing
# INSTANCE - worm number
# TIME1 - fast advance time
# TIME2 - slow advance time
# COLOR - palette color index
# STEP - advance step for the slow advancing sequencer
# SEQ1 - fast advancing sequencer number
# SEQ2 - slow advancing sequencer number
[[worm INSTANCE TIME1 TIME2 COLOR STEP SEQ1 SEQ2
  # start up a worm
  [worm-start-INSTANCE]
    # allocate a sequencer 1/6 the display width for the small/fast sequence
    {seq-SEQ1},<width0>
    <seq-type>

    # allocate a sequencer for the remaining 5/6 display width to swing the sequence
    {seq-SEQ2},<width2>
    <seq-type>

    # schedule the draw macro
    (worm-draw-INSTANCE TIME1)
    
    # schedule the advance macro
    (worm-advance-INSTANCE TIME2)

  # draw the next animation frame
  [worm-draw-INSTANCE]
    # advance the 1/6 sequencer
    <seq-SEQ1>,<GET-NEXT-NO-SAVE>
    sequence

    # add the two sequencers
    # get and set the offset,window to fill in for this frame
    <seq-SEQ1>,<ADD-SEQ>,<seq-SEQ2>
    sequence-next-window

    # set drawing window
    position

    # place the palette color
    COLOR
    palette-color

    # set the fade type
    <fade-type>

    # fill the whole space
    flood
    
    # restore the viewport
    reset

  # macro to sequence the 5/6 sequencer at a variable rate
  [worm-advance-INSTANCE]
    # advance the sequencer
    <seq-SEQ2>,<GET-NEXT-NO-SAVE>,STEP
    sequence
]]

