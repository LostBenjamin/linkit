# ----------------------------------------------------------------
# Inchworm
# 5 rainbow wandering inchworms
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

%run-macro inchworm-app

$render-time 100
$width0 `<NUM-LEDS> * 1 / 6`
$width1 <NUM-LEDS>
$width2 `<NUM-LEDS> * 5 / 6`
$time1 50
$time2 200
$seq-type sqs
$fade-type ffd

[inchworm-app 10]
  application

  # make colors intermingle
  <DRAW-PLUS>
  draw-mode

  # schedule the renderer
  (render <render-time>)
  
  # start the worms
  (worms-start)
  
  # start the palette color sequencer
  (hsl-sequencer-start)

# start up all worms
[worms-start]
  # expand 5 wornm start meta-templates
  (((worm-start 3)))

# expand 5 meta-templates for worms, computing the various values
(((worm 3 `<time1>-(X*3)` `<time2>-(X*3)` `X*2` `X+1` X `X+3` `X+6`)))

# template for starting worms
# X - worm number
[[worm-start X
  (worm-start-X)
]]

# template for all worm processing
# X - worm number
# TIME1 - draw time / fast advance time
# TIME2 - slow advance time
# COLOR - palette color index
# STEP - advance step for the slow advancing sequencer
# SEQ1 - fast advancing sequencer number
# SEQ2 - macro math sequencer number
# SEQ3 - slow advancing sequencer number
[[worm X TIME1 TIME2 COLOR STEP SEQ1 SEQ2 SEQ3
  # start up a worm
  [worm-start-X]
    # allocate a sequencer 1/6 the display width for the small/fast sequence
    {seq-SEQ1},<width0>
    <seq-type>

    # allocate a sequencer for performing macro math on advance
    {seq-SEQ2},<width1>
    <seq-type>

    # allocate a sequencer for the remaining 5/6 display width to swing the sequence
    {seq-SEQ3},<width2>
    <seq-type>

    # schedule the draw macro
    (worm-draw-X TIME1)
    
    # schedule the advance macro
    (worm-advance-X TIME2)

  # use the macro math sequencer to draw the next animation frame
  [worm-draw-X]
    # use a macro to compute the next position
    <seq-SEQ2>,<MACRO-SEQ>,<worm-math-X>

    # get and set the offset,window to fill in for this frame
    sequence-next-window
    position

    # place the palette color
    COLOR
    palette-color

    # set the fade type
    <fade-type>

    # fill the whole space
    flood
    
    # restore the viewport
    reset

  # macro used by the sequencer to compute the next position
  [worm-math-X]
    # advance the 1/6 sequencer and push onto stack
    <seq-SEQ1>
    sequence
    push
    
    # advance the 5/6 sequencer and push onto stack
    <seq-SEQ3>,<GET-CURRENT>
    sequence
    push

    # add the two values, leaving the result in arg0
    add

  # macro to sequence the 5/6 sequencer at a variable rate
  [worm-advance-X]
    # advance the sequencer, leaving the result in arg0
    <seq-SEQ3>,0,STEP
    sequence
]]
