# fancy - 6 part rainbow consine swing
# trippy - rainbow wandering pixels with mirror

%include symbols
%run-macro 11

$fade-rate 8000
$fade-type sfd
$sequencer-type swc
$hsl-angle 60
$hsl-step 2
$hsl-time 100
$base-timing 150
$timing-gap 1
$render-time <base-timing>
$draw-mode <DRAW-MIX>
$max <NUM-LEDS>
$max-half <max>
$render-time2 50
$sequence-time 20
$fade-type2 ffd
$wander-range 3
$wander-neg-range `-(<wander-range> / 2)`
$brightness 100

[fancy 10]
    app
    <DRAW-WRITE>:drw
    <CONFIG-FADE-RATE>,<fade-rate>:cfg
    <render-time>,<render>:sch
    (setup-swingers)
    (setup-hsl-sequencer)

[render !]
    flu

[setup-swingers]
    (((start 6)))

[setup-hsl-sequencer]
    {seq-hsl},360:seq
    <hsl-time>,<hsl-sequencer>:sch

[hsl-sequencer]
    <seq-hsl>,0,<hsl-step>:seq
    sto
    <hsl-angle>:rcl
    csh

(((swinger 6 15)))

[[start X
    (setup-swinger-X)
]]

[[swinger X WIDTH
    [setup-swinger-X]
        {seq-X},`X*WIDTH+WIDTH`,`X*WIDTH`:<sequencer-type>
        `<base-timing> + (<seq-X> * <timing-gap>)`,<swinger-X>:sch

    [swinger-X]
        <seq-X>:snw
        pos
        <seq-X>:pal
        <fade-type>
        flo
        rst
]]

[trippy 11]
    app
    <draw-mode>:drw
    <brightness>:lev
    (setup-rendering2)
    (((start2 6)))
    (setup-hsl-sequencer)

[setup-rendering2]
    <render-time2>,<render2>:sch

[render2]
    mir
    flu

(((wanderer 6)))

[[start2 X
    (start2-X)
]]

[[wanderer X
    [start2-X]
        <seq-X>,<max>,0:seq
        <sequence-time>,<sequence-X>:sch

    [sequence-X]
        <seq-X>,<MACRO-SEQ>,<math-X>:seq
        pos
        X:pal
        <fade-type2>
        rst

    [math-X]
        <wander-range>:rng:psh
        <wander-neg-range>:psh
        add
        <seq-X>,<GET-CURRENT>:seq:psh
        add
        <max-half>:psh
        mod
]]

