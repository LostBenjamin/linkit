# ----------------------------------------------------------------
# Swinging Color Bars v.1.0 2/0/2019 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer
%play-macro bars-app

$num-instances 4
$instance-width 12

$width-time 1250

%allow-mutability
$HSL-ANGLE `360/<num-instances>`
$HSL-TIME 1000

$base-next-time 70
$draw-mode <DRAW-WRITE>
$brightness <BRIGHTNESS-AUTO>
$render-time <base-next-time>
$fade-rate 9000
$low-limit `<instance-width>/2`
$high-limit `<NUM-LEDS>-<low-limit>`
$add-time 3

[bars-app 10]
    start-application
 
<<< <draw-mode> != <DRAW-WRITE>
    # set the preset draw mode
    <draw-mode>
    set-draw-mode
>>>

    # set the preset brightness level
    <brightness>
    set-brightness

    # start the macro instances
    [[[start <num-instances>]]]

    # start up the palette color sequencer
    (hsl-sequencer-start)

    # schedule the display rendering
    (render-all <render-time>)

    (start-width-sequencer)

[start-width-sequencer]
  {seq-width}, `1+<instance-width>`, `int(<instance-width>/4)`
  start-swing-sequence
  (advance-width-sequence <width-time>)

[advance-width-sequence]
  {seq-width},<GET-NEXT>
  advance-sequence

[render-all]
    {{{
        <FAST-ERASE-BUFFER>
        erase-buffer
        [[[draw <num-instances>]]]
    }}}

(((bar <num-instances> INSTANCE `INSTANCE*<add-time>+<base-next-time>`)))

[[bar INSTANCE PALETTE TIME

    [start-INSTANCE]
        {seq-INSTANCE},<high-limit>,<low-limit>
        start-cosine-sequence

        (advance-INSTANCE TIME)

    [advance-INSTANCE]
        {seq-INSTANCE},<GET-NEXT>
        advance-sequence

    [draw-INSTANCE]
        <low-limit>
        push

        {seq-INSTANCE},<GET-COMPUTED>
        advance-sequence
        push
	sub-equals

        {seq-width},<GET-CURRENT>
        get-sequence
        recall

        set-position
        {{
            PALETTE
            add-palette-color
            darken
            darken
            <FILL-COLORS>
            fill
        }}
]]

