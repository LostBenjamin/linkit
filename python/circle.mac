# ----------------------------------------------------------------
# 
# 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

$draw-mode <DRAW-MIX>
$brightness <BRIGHTNESS-AUTO>

$render-time 250
$advance-time 75
$draw-time 75
$fade-type sfd

$color 0

$factor 2
$max 16
$margin 1
$upper `<max>-<margin>`
$lower <margin>
$high-limit `<upper>*<factor>`
$low-limit `<lower>*<factor>`

# notes:
# using mixed draw mode so the colors intermingle
# use a fast fade to avoid clutter

%run-macro circle-app

# set up the application on macro #10
[circle-app 10]
    application
 
    # set the preset draw mode
    <draw-mode>
    draw-mode

    # set the preset brightness level
    <brightness>
    level

    # start up the palette color sequencer
    (hsl-sequencer-start)

    # schedule the sequencer advance and draw
    (circle-advance <advance-time>)
    (circle-draw <draw-time>)

    # set up the sequencers
    (circle-setup)

    # schedule the display rendering
    (render <render-time>)

[circle-setup]
    # allocate a sequencer for X
    {seq-x},<high-limit>,<low-limit>
    wheel-cosine-sequencer

    # allocate a sequencer for Y
    {seq-y},<high-limit>,<low-limit>
    wheel-sine-sequencer

    # allocate a sequencer for drawing X
    {seq-draw-x},<high-limit>
    wheel-sequencer

    # allocate a sequencer for drawing Y
    {seq-draw-y},<high-limit>
    wheel-sequencer

[circle-advance]
    <seq-x>,<GET-NEXT>
    sequence
    <seq-y>,<GET-NEXT>
    sequence

[circle-math-x]
    <seq-x>,<GET-COMPUTED>
    sequence
    push
    <factor>
    push
    div

[circle-math-y]
    <seq-y>,<GET-COMPUTED>
    sequence
    push
    <factor>
    push
    div

[circle-draw]
    <seq-draw-x>,<MACRO-SEQ>,<circle-math-x>
    sequence
    push

    <seq-draw-y>,<MACRO-SEQ>,<circle-math-y>
    sequence
    push

    recall
    xy-position

    # need to abort if bad position, or mask the next command

    <color>
    palette-color
    <fade-type>

    reset

