# ----------------------------------------------------------------
# 
# 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

$draw-mode <DRAW-MIX>
$brightness <BRIGHTNESS-AUTO>

$render-time 250
$advance-time-x 75
$advance-time-y 80
$step-time 75
$fade-type sfd

$color 0

$factor 4
$max 16
$margin 2
$upper `<max>-<margin>`
$lower <margin>
$high-limit `<upper>*<factor>`
$low-limit `<lower>*<factor>`

# notes:
# using mixed draw mode so the colors intermingle
# use a fast fade to avoid clutter

%run-macro circle-app

# set up the application on macro #10
[circle-app 10]
    application
 
    # set the preset draw mode
    <draw-mode>
    draw-mode

    # set the preset brightness level
    <brightness>
    level

    # start up the palette color sequencer
    (hsl-sequencer-start)

    # schedule the sequencer advance and draw
    (circle-advance-x <advance-time-x>)
    (circle-advance-y <advance-time-y>)
    (circle-step <step-time>)

    # set up the sequencers
    (circle-setup)

    # schedule the display rendering
    (render <render-time>)

[circle-setup]
    # allocate a sequencer for X
    {seq-x},<high-limit>,<low-limit>
    wheel-cosine-sequencer

    # allocate a sequencer for Y
    {seq-y},<high-limit>,<low-limit>
    wheel-sine-sequencer

    # allocate a sequencer for drawing X
    {seq-step-x},<high-limit>
    wheel-sequencer

    # allocate a sequencer for drawing Y
    {seq-step-y},<high-limit>
    wheel-sequencer

[circle-advance-x]
    <seq-x>,<GET-NEXT>
    sequence

[circle-advance-y]
    <seq-y>,<GET-NEXT>
    sequence

[circle-math-x]
    <seq-x>,<GET-COMPUTED>
    sequence
    push
    <factor>
    push
    div

[circle-math-y]
    <seq-y>,<GET-COMPUTED>
    sequence
    push
    <factor>
    push
    div

[circle-draw]
    <color>
    palette-color
    <fade-type>

[circle-step]
    <circle-draw>
    push

    <seq-step-y>,<MACRO-SEQ>,<circle-math-y>
    sequence
    push

    <seq-step-x>,<MACRO-SEQ>,<circle-math-x>
    sequence
    push

    recall
    xy-position

    reset

