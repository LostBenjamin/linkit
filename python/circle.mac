# ----------------------------------------------------------------
# 
# 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

$draw-mode <DRAW-WRITE>
$brightness <BRIGHTNESS-AUTO>
$render-time 160
$fade-type sfd
$max 16
$fade-rate 9000

# notes:
# using mixed draw mode so the colors intermingle
# use a fast fade to avoid clutter

%run-macro circle-app

# set up the application on macro #10
[circle-app 10]
    application
 
    # set the preset draw mode
    <draw-mode>
    draw-mode

    <CONFIG-FADE-RATE>,<fade-rate>
    configure

    # set the preset brightness level
    <brightness>
    level

    # start up the palette color sequencer
    (hsl-sequencer-start)

    # start up the circle processing
    (((circle-init 2)))

    # schedule the display rendering
    (render <render-time>)

((circle 0 1 3 75 80 80))
((circle 1 3 3 80 75 80))
#((circle 2 5 3 70 80 80))

[[circle-init INSTANCE
  (circle-init-INSTANCE)
]]

[[circle INSTANCE MARGIN FACTOR TIMEX TIMEY TIMES

    [circle-init-INSTANCE]
        # set up the sequencers
        (circle-setup-INSTANCE)

        # schedule the sequencer advance and draw
        (circle-advance-x-INSTANCE TIMEX)
        (circle-advance-y-INSTANCE TIMEY)
        (circle-step-INSTANCE TIMES)

    [circle-setup-INSTANCE]
        # allocate a sequencer for X
        {seq-x-INSTANCE},`(<max>-MARGIN)*FACTOR`,`MARGIN*FACTOR`
        wheel-cosine-sequencer

        # allocate a sequencer for Y
        {seq-y-INSTANCE},`(<max>-MARGIN)*FACTOR`,`MARGIN*FACTOR`
        wheel-sine-sequencer

        # allocate a sequencer for drawing X
        {seq-step-x-INSTANCE},`(<max>-MARGIN)*FACTOR`
        wheel-sequencer

        # allocate a sequencer for drawing Y
        {seq-step-y-INSTANCE},`(<max>-MARGIN)*FACTOR`
        wheel-sequencer

    [circle-advance-x-INSTANCE]
        <seq-x-INSTANCE>,<GET-NEXT>
        sequence

    [circle-advance-y-INSTANCE]
        <seq-y-INSTANCE>,<GET-NEXT>
        sequence

    [circle-math-x-INSTANCE]
        <seq-x-INSTANCE>,<GET-COMPUTED>
        sequence
        push
        FACTOR
        push
        div

    [circle-math-y-INSTANCE]
        <seq-y-INSTANCE>,<GET-COMPUTED>
        sequence
        push
        FACTOR
        push
        div

    [circle-draw-INSTANCE]
        `INSTANCE*3`
        palette-color
        <fade-type>

    [circle-step-INSTANCE]
        <circle-draw-INSTANCE>
        push

        <seq-step-y-INSTANCE>,<MACRO-SEQ>,<circle-math-y-INSTANCE>
        sequence
        push

        <seq-step-x-INSTANCE>,<MACRO-SEQ>,<circle-math-x-INSTANCE>
        sequence
        push

        recall
        xy-position

        reset
]]

