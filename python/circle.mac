# ----------------------------------------------------------------
# 
# 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

$num-instances 2
$brightness <BRIGHTNESS-AUTO>
$render-time 120
$fade-type sfd
$max 16
$fade-rate 9000
$factor 8
$base-timing 25
$base-margin 1
$timing-offset 5

# notes:
# using mixed draw mode so the colors intermingle
# use a fast fade to avoid clutter

%play-macro circle-app

# set up the application on macro #10
[circle-app 10]
    start-application
 
    # set the preset brightness level
    <brightness>
    set-brightness

    # start up the palette color sequencer
    (hsl-sequencer-start)

    (circle-app-startup)

[circle-app-startup]
    # start up the circle processing
    [[[circle-init <num-instances>]]]

    # schedule the display rendering
    (circle-render <render-time>)

[circle-render]
  <EFFECTS-PROCESSING>
  refresh-display

((circle 0 2 <factor> `<base-timing>+<timing-offset>` <base-timing> <base-timing>))
((circle 1 3 <factor> `<base-timing>-<timing-offset>` <base-timing> <base-timing>))

[[circle INSTANCE MARGIN FACTOR TIMEX TIMEY TIMES

    [circle-init-INSTANCE]
        # set up the sequencers
        (circle-setup-INSTANCE)

        # schedule the sequencer advance and draw
        (circle-schedule-INSTANCE)

    [circle-schedule-INSTANCE]
        (circle-advance-x-INSTANCE TIMEX)
        (circle-advance-y-INSTANCE TIMEY)
        (circle-step-INSTANCE TIMES)

    [circle-setup-INSTANCE]
        (circle-setup-x-INSTANCE)
        (circle-setup-y-INSTANCE)

    [circle-setup-x-INSTANCE]
        # allocate a sequencer for X
        {seq-x-INSTANCE},`(<max>-MARGIN)*FACTOR`,`MARGIN*FACTOR`
        start-cosine-sequence

        # allocate a sequencer for drawing X
        {seq-step-x-INSTANCE},`(<max>-MARGIN)*FACTOR`
        start-wheel-sequence

    [circle-setup-y-INSTANCE]
        # allocate a sequencer for Y
        {seq-y-INSTANCE},`(<max>-MARGIN)*FACTOR`,`MARGIN*FACTOR`
        start-sine-sequence

        # allocate a sequencer for drawing Y
        {seq-step-y-INSTANCE},`(<max>-MARGIN)*FACTOR`
        start-wheel-sequence

    [circle-advance-x-INSTANCE]
        <seq-x-INSTANCE>,<GET-NEXT>
        advance-sequence

    [circle-advance-y-INSTANCE]
        <seq-y-INSTANCE>,<GET-NEXT>
        advance-sequence

    [circle-math-x-INSTANCE]
        <seq-x-INSTANCE>,<GET-COMPUTED>
        get-sequence
        push
        FACTOR
        push
        div-equals

    [circle-math-y-INSTANCE]
        <seq-y-INSTANCE>,<GET-COMPUTED>
        get-sequence
        push
        FACTOR
        push
        div-equals

    [circle-draw-INSTANCE]
        `INSTANCE*3`
        add-palette-color
        <fade-type>

    # push the X and Y position 
    [circle-advance-INSTANCE]
        <seq-step-x-INSTANCE>,<MACRO-SEQ>,<circle-math-x-INSTANCE>
        advance-sequence
        push

        <seq-step-y-INSTANCE>,<MACRO-SEQ>,<circle-math-y-INSTANCE>
        advance-sequence
        push

    [circle-step-INSTANCE]
        # push macro for drawing
        <circle-draw-INSTANCE>
        push

        # push X,Y position
        (circle-advance-INSTANCE)

        recall
        {{
            set-xy-position
        }}
]]

