# ----------------------------------------------------------------
# 
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

$draw-mode <DRAW-WRITE>
$brightness <BRIGHTNESS-AUTO>
$render-time <ball-time>
$fade-rate 9000
$ball-time 60
$box-width 15
$box-time 120
$num-boxes 3
$balls-per-box 2
$box-instance-time 30
$ball-instance-time 10
$refresh-time 80
$box-sequencer swing-sequencer
$ball-sequencer swing-sequencer
$box-step 0
$ball-step 0

$bgtype-line 1
$bgtype-points 2
$background-type <bgtype-line>

#%allow-mutability
#$HSL-ANGLE `int(360/(<num-boxes>*<balls-per-box>))`

%play-macro app

[app 10]
    start-application
 
    # set the preset draw mode
    <draw-mode>
    set-draw-mode

    <CONFIG-FADE-RATE> = <fade-rate>

    # set the preset brightness level
    <brightness>
    set-brightness

    [[[box-start <num-boxes>]]]

    (boxes-render <render-time>)
    (render <refresh-time>)

    (hsl-sequencer-start)

[boxes-render]
    erase-buffer
    [[[box-render <num-boxes>]]]

(((box <num-boxes> `<box-instance-time>*INSTANCE+<box-time>` <box-width> `<NUM-LEDS>-<box-width>` <box-sequencer>)))

(((create-balls-for-box <num-boxes>)))

[[create-balls-for-box BOX
    (((ball <balls-per-box> BOX `<num-boxes>*INSTANCE*<ball-instance-time>+<ball-time>` <box-width> <ball-sequencer> `BOX+INSTANCE+<RED>`)))
]]


###### BOX ######

[[box INSTANCE SCHEDULE WIDTH MAX SEQ-TYPE

    [box-start-balls-INSTANCE]
        (((ball-start-all <balls-per-box> INSTANCE)))

    [box-start-INSTANCE]
        {seq-box-INSTANCE}, MAX
        SEQ-TYPE
        (box-start-balls-INSTANCE)
        (box-step-INSTANCE SCHEDULE)

    [box-step-INSTANCE]
        {seq-box-INSTANCE},<GET-NEXT>,<box-step>
        advance-sequence

    [box-erase-INSTANCE]
        (box-set-window-INSTANCE)
        INSTANCE
        push
        (box-draw-background)
        
    [box-render-INSTANCE]
        (box-erase-INSTANCE)
        (((ball-render-each <balls-per-box> INSTANCE)))

    [box-set-window-INSTANCE]
        {seq-box-INSTANCE}, <GET-CURRENT>
        store
        (box-set-window)
]]

[box-draw-background]
<<< <background-type> == <bgtype-points>
     dark-gray

    <box-width>
    set-position
    {{
        dark-gray
    }}
>>>
<<< <background-type> == <bgtype-line>
    pop
    add-palette-color
    darken
    darken
    darken
    fill
>>>

[box-set-window]
    recall
    get-sequence
    push

    recall
    set-offset

    <box-width>
    push
    add-equals

    set-window

[[ball-start-all BALL BOX
    (ball-start-BOX-BALL)
]]

[[ball-render-each BALL BOX
    {{
        (box-set-window-BOX)
        (ball-render-BOX-BALL)
    }}
]]


###### BALL ######

[[ball INSTANCE BOX SCHEDULE WIDTH SEQ-TYPE COLOR

    [ball-start-BOX-INSTANCE]
        {seq-ball-BOX-INSTANCE}, WIDTH
        SEQ-TYPE

        (ball-step-BOX-INSTANCE SCHEDULE)

    [ball-step-BOX-INSTANCE]
        {seq-ball-BOX-INSTANCE},<GET-NEXT>,<ball-step>
        advance-sequence

    [ball-render-BOX-INSTANCE]
        {seq-ball-BOX-INSTANCE}, <GET-CURRENT>
        get-sequence

        set-position
        {{
            COLOR
            add-palette-color
        }}
]]


