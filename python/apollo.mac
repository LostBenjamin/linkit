
%include symbols
%run-macro trippy

$fade-rate 8000
$fade-type sfd
$sequencer-type swc
$base-timing 150
$timing-gap 1
$render-time <base-timing>
$max <NUM-LEDS>
$max-half <max>

# ----------------------------------------------------------------
# Fancy
# 6 part rainbow consine swing
# ----------------------------------------------------------------

[fancy 10]
    app
    <DRAW-WRITE>
    drw
    <CONFIG-FADE-RATE>,<fade-rate>
    cfg
    (render <render-time>)
    (hsl-sequencer-start)
    (((start 6)))

[render !]
    flu

(((swinger 6 15)))

[[start X
    (setup-swinger-X)
]]

[[swinger X WIDTH
    [setup-swinger-X]
        {seq-X},`X*WIDTH+WIDTH`,`X*WIDTH`
        <sequencer-type>
        (swinger-X `<base-timing>+(<seq-X>*<timing-gap>)`)

    [swinger-X]
        <seq-X>
        snw
        pos
        <seq-X>
        pal
        <fade-type>
        flo
        rst
]]


# ----------------------------------------------------------------
# Trippy
# rainbow wandering pixels with mirror
# ----------------------------------------------------------------

$wanderer-draw-mode <DRAW-MIX>
$wanderer-brightness 100
$wanderer-render-time 50
$wanderer-advance-time 20
$wanderer-fade-type ffd
$wander-range 3
$wander-neg-range `-(<wander-range> / 2)`
$wanderer-max <NUM-LEDS>
$wanderer-max-half <wanderer-max>

[trippy]
    app
    <wanderer-draw-mode>
    drw
    <wanderer-brightness>
    lev
    (((wanderer-setup 6)))
    (hsl-sequencer-start)
    (wanderer-render <wanderer-render-time>)

[wanderer-render]
    mir
    flu

(((wanderer 6)))

[[wanderer-setup X
    (wanderer-setup-X)
]]

[[wanderer X
    [wanderer-setup-X]
        {seq-X},<wanderer-max>
        seq
        (wanderer-advance-X <wanderer-advance-time>)

    [wanderer-advance-X]
        <seq-X>,<MACRO-SEQ>,<wanderer-math-X>
        seq
        pos
        X
        pal
        <wanderer-fade-type>
        rst

    [wanderer-math-X]
        <wander-range>
        rng
        psh
        <wander-neg-range>
        psh
        add
        <seq-X>,<GET-CURRENT>
        seq
        psh
        add
        <wanderer-max-half>
        psh
        mod
]]


%include hsl-sequencer


