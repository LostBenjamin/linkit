
# ----------------------------------------------------------------
# Groups of Objects animation
# ----------------------------------------------------------------

%include symbols
%include hsl-sequencer

$render-time <object-time>
$refresh-time <object-time>

$num-groups 2
$group-width 8
$group-time <object-time>

$objects-per-group 2
$object-time 150
$object-instance-time 5
$object-sequencer sine-sequencer
$object-step <DEFAULT-STEP>
$object-darkening 2
$smooth-factor 4

$right 0
$right-start 0
$right-width <group-width>

$left 1
$left-start 56
$left-width <group-width>

%play-macro app

[app 10]
    start-application
    (hsl-sequencer-start)
    (objects-groups-start)

((group <right> <group-time> <right-start> <right-width> <objects-per-group>))
((group <left>  <group-time> <left-start>  <left-width>  <objects-per-group>))

((objects-groups-start-template <num-groups> <group-width> <group-time> <render-time> <objects-per-group> <object-time> <object-sequencer> <object-step> <object-instance-time> <object-darkening> <smooth-factor>))

# -----------------------------------------------

[[objects-groups-start-template NUM-GROUPS GROUP-WIDTH GROUP-TIME RENDER-TIME OBJECTS-PER-GROUP OBJECT-TIME OBJECT-SEQUENCER OBJECT-STEP OBJECT-INSTANCE-TIME OBJECT-DARKEN FACTOR
    [objects-groups-start]
        [[[group-start NUM-GROUPS]]]
        (groups-render GROUP-TIME)
        (render RENDER-TIME)

    [groups-render]
        [[[group-render NUM-GROUPS]]]

    (((create-objects-for-group NUM-GROUPS OBJECTS-PER-GROUP OBJECT-TIME GROUP-WIDTH OBJECT-SEQUENCER OBJECT-STEP OBJECT-INSTANCE-TIME OBJECT-DARKEN FACTOR)))
]]

[[create-objects-for-group GROUP-NUM OBJECTS-PER-GROUP OBJECT-TIME GROUP-WIDTH OBJECT-SEQUENCER OBJECT-STEP OBJECT-INSTANCE-TIME OBJECT-DARKEN FACTOR
    (((object OBJECTS-PER-GROUP GROUP-NUM `(OBJECT-INSTANCE-TIME*INSTANCE)+OBJECT-TIME` GROUP-WIDTH OBJECT-SEQUENCER `(GROUP-NUM*OBJECTS-PER-GROUP)+INSTANCE` OBJECT-STEP OBJECT-DARKEN FACTOR)))
]]

# -----------------------------------------------

[[group INSTANCE SCHEDULE START WIDTH OBJECTS-PER-GROUP
    [group-start-objects-INSTANCE]
        (((object-start-all OBJECTS-PER-GROUP INSTANCE)))

    [group-start-INSTANCE]
        (group-start-objects-INSTANCE)

    [group-render-INSTANCE]
        (group-set-window-INSTANCE)
        {{
            erase-buffer
        }}
        (((object-render-each OBJECTS-PER-GROUP INSTANCE)))

    [group-set-window-INSTANCE]
        START, WIDTH
        set-position
]]

# -----------------------------------------------

[[object-start-all OBJECT GROUP
    (object-start-GROUP-OBJECT)
]]

[[object-render-each OBJECT GROUP
    (object-render-GROUP-OBJECT)
]]

[[object INSTANCE GROUP SCHEDULE WIDTH SEQ-TYPE COLOR STEP DARKEN-LEVEL FACTOR
    [object-start-GROUP-INSTANCE]
        {seq-object-GROUP-INSTANCE}, `WIDTH*FACTOR-1`
        SEQ-TYPE

        (object-step-GROUP-INSTANCE SCHEDULE)

    [object-step-GROUP-INSTANCE]
        {seq-object-GROUP-INSTANCE}, <GET-NEXT>, STEP
        advance-sequence

    [object-render-GROUP-INSTANCE]
        (group-set-window-GROUP)

        {seq-object-GROUP-INSTANCE}, <GET-COMPUTED>
        get-sequence
        push
        FACTOR
        push
        divide-equals
        set-position
        {{
            COLOR
            add-palette-color
            DARKEN-LEVEL
            darken
        }}
]]

